apply plugin: 'wrapper'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'shadow'
apply plugin: 'project-report'
apply plugin: 'cobertura'
apply plugin: 'coveralls'

group = 'com.airbnb.plog'
version = '3.0.0-WIP'
mainClassName = 'com.airbnb.plog.App'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { jcenter() }

dependencies {
    compile 'org.projectlombok:lombok:1.12.4'
    compile 'io.netty:netty-handler:4.0.17.Final'
    compile 'io.netty:netty-codec:4.0.17.Final'
    compile 'org.javassist:javassist:3.18.1-GA'
    compile 'com.google.guava:guava:16.0.1'
    compile 'com.typesafe:config:1.2.0'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'org.slf4j:slf4j-log4j12:1.7.6'
    compile 'org.xerial.snappy:snappy-java:1.1.0.1'
    compile('org.apache.kafka:kafka_2.10:0.8.0') {
        exclude module: 'zookeeper'
        exclude module: 'zkclient'
        exclude module: 'jmxri'
        exclude module: 'jmxtools'
        exclude module: 'slf4j-simple'
    }
    testCompile 'org.codehaus.groovy:groovy-all:2.2.1'
    testCompile 'junit:junit:4.11'
}

jar { manifest { attributes 'Main-Class': 'com.airbnb.plog.App' } }

shadow { artifactSet { exclude '*:lombok:*' } }

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

run {
    maxHeapSize = '128M'
    jvmArgs = [
        '-XX:+UseConcMarkSweepGC',
        '-XX:+AggressiveOpts',
        '-XX:+UseCompressedOops',
        '-XX:+UseFastEmptyMethods',
        '-XX:+UseFastAccessorMethods',
        '-XX:CompileThreshold=1500',
    ]
}

test {
    // gradle screws up SLF4J for that test. Use IntelliJ :)
    exclude 'com/airbnb/plog/EndOfPipeline*'
}

cobertura.coverageFormats = ['html', 'xml']

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.2.5'
    }
}
